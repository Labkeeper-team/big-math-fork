
plugins {
    id 'org.sonarqube' version '2.7'
    id 'java'
    id 'eclipse'
    id 'maven'
    id 'signing'
    id 'jacoco'
    id 'maven-publish'
}

publishing {
    repositories {
        maven {
            name = "mymavenrepo"
            url = "https://mymavenrepo.com/repo/lMX1KgJf0wS9ayeYirfY/"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
    publications {
        register("jar", MavenPublication) {
            from(components["java"])
        }
    }
}

group = 'ch.obermuhlner.fork'
version = '2.3.2-fork-1.1'
archivesBaseName = 'big-math'

ext {
    moduleName = "ch.obermuhlner.math.big"

    if (!project.hasProperty("ossrhUsername")) {
        ossrhUsername = "undefined"
    }
    if (!project.hasProperty("ossrhPassword")) {
        ossrhPassword="undefined"
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

compileJava   {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

dependencies {
    testCompile 'junit:junit:4.12'
}

test {
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "big-math"
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

jar {
    manifest {
        attributes(
                "Automatic-Module-Name": moduleName,
                "Bundle-ManifestVersion": 2,
                "Bundle-Name": archivesBaseName,
                "Bundle-SymbolicName": moduleName,
                "Bundle-Version": version,
                "Export-Package": "ch.obermuhlner.math.big, ch.obermuhlner.math.big.stream")
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    //archives jar // no longer needed tp add - will lead to duplicate
    archives sourcesJar
    archives javadocJar
}
